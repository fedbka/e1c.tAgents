
#Область ПрограммныйИнтерфейс

Функция task_POST(Запрос) Экспорт

	ТипСодержимого = Запрос.Заголовки.Получить("Content-Type");
	
	Если ТипСодержимого <> "application/json" Тогда
		Возврат Новый HTTPСервисОтвет(400, Текст_Ошибка_НекорректныйФорматДанных());
	КонецЕсли;
		
	СодержаниеЗапросаJSON = Запрос.ПолучитьТелоКакСтроку();
	СодержаниеЗапроса = ТелеграмАгентыКлиентСервер.ПреобразоватьВСоответствие(СодержаниеЗапросаJSON);

	Если СодержаниеЗапроса = Неопределено Тогда
		Возврат Новый HTTPСервисОтвет(400, Текст_Ошибка_НекорректныйФорматДанных());
	КонецЕсли;
	
	Команда = СодержаниеЗапроса.Получить("@type");
	
	Если Команда = Неопределено Тогда
		Возврат Новый HTTPСервисОтвет(400, Текст_Ошибка_НекорректныйФорматДанных());
	КонецЕсли;
	
	ИдентификаторЗапроса = ТелеграмАгенты.СохранитьВнешнийЗапросАгенту(,СодержаниеЗапросаJSON, Команда);
	
	Если ИдентификаторЗапроса = Неопределено Тогда
		Возврат Новый HTTPСервисОтвет(400, Текст_Ошибка_ЗапросНеОбработан());
	КонецЕсли;
	
	СодержаниеОтвета = Новый Соответствие;
	СодержаниеОтвета.Вставить("id", ИдентификаторЗапроса);
	СодержаниеОтвета.Вставить("status", ТелеграмАгентыКлиентСервер.СтатусНовыйЗапрос());
	
	СодержаниеОтветаJSON = ТелеграмАгентыКлиентСервер.ПреобразоватьВJSON(СодержаниеОтвета);
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(СодержаниеОтветаJSON);
			
	Возврат Ответ;
	
КонецФункции

Функция taskstatus_GET(Запрос) Экспорт
	
	Идентификатор = Запрос.ПараметрыURL.Получить("id");
	
	Если Идентификатор = Неопределено Тогда
		Возврат Новый HTTPСервисОтвет(400, Текст_Ошибка_НеУказанИдентификаторЗапроса());
	КонецЕсли;
	
	СодержаниеОтветаJSON = Текст_Ответ_СтатусЗапроса(Идентификатор);
	
	Если СодержаниеОтветаJSON = Неопределено Тогда
		Возврат Новый HTTPСервисОтвет(400, Текст_Ошибка_ИдентификаторЗапросаНеНайден());
	КонецЕсли;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(СодержаниеОтветаJSON);
	
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Текст_Ошибка_НекорректныйФорматДанных()
	
	Возврат "request body not specified or it incorrect format (required JSON with ""@type"" field)";
	
КонецФункции

Функция Текст_Ошибка_ЗапросНеОбработан()
	
	Возврат "request processing error";
	
КонецФункции

Функция Текст_Ошибка_НеУказанИдентификаторЗапроса()

	Возврат "request id not specified";
	
КонецФункции

Функция Текст_Ошибка_ИдентификаторЗапросаНеНайден()
	
	Возврат "request id not found";
	
КонецФункции

Функция Текст_Ответ_СтатусЗапроса(Идентификатор)
	
	СостояниеЗапроса = ТелеграмАгенты.ДанныеВнешнихЗапросов(, Идентификатор);
	
	Если СостояниеЗапроса = Неопределено
		Тогда Возврат Неопределено;
	КонецЕсли;
	
	СостояниеЗапроса = СостояниеЗапроса[0];
	
	СодержаниеОтвета = Новый Соответствие;
	СодержаниеОтвета.Вставить("id", СостояниеЗапроса.Идентификатор);
	СодержаниеОтвета.Вставить("status", СостояниеЗапроса.Статус);
	
	Если СостояниеЗапроса.ОтветПолучен Тогда
		СодержаниеОтвета.Вставить("answer", СостояниеЗапроса.СодержаниеОтветаJSON);
		СодержаниеОтвета.Вставить("answer_date", СостояниеЗапроса.ДатаПолученияОтвета);
	КонецЕсли;
	
	СодержаниеОтветаJSON = ТелеграмАгентыКлиентСервер.ПреобразоватьВJSON(СодержаниеОтвета);
	
	Возврат СодержаниеОтветаJSON;
	
КонецФункции

#КонецОбласти