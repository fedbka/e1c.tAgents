
#Область ПрограммныйИнтерфейс

Функция task_POST(Запрос)
	
	ТипСодержимого = Запрос.Заголовки.Получить("Content-Type");
	
	Если ТипСодержимого = "application/json" Тогда
	
		ТелоЗапросаJSON = Запрос.ПолучитьТелоКакСтроку();
		
		ПреобразованноеТелоЗапроса = ТелеграмАгентыКлиентСервер.ПреобразоватьВСоответствие(ТелоЗапросаJSON);
		
		Если ПреобразованноеТелоЗапроса = Неопределено
			ИЛИ ПреобразованноеТелоЗапроса.Получить("@type") = Неопределено Тогда
			Ответ = Новый HTTPСервисОтвет(400);
			Ответ.Причина = "request body not specified or it incorrect format (required JSON with ""@type"" field)";
			Возврат Ответ;
		КонецЕсли;
		
		ИдентификаторЗапроса = ТелеграмАгентыHTTPЗапросы.taskPost(Запрос, ТелоЗапросаJSON, ПреобразованноеТелоЗапроса);
		
		Если ИдентификаторЗапроса = Неопределено Тогда
			Ответ = Новый HTTPСервисОтвет(404);
			Ответ.Причина = "request processing error";
			Возврат Ответ;
		КонецЕсли;
		
		Результат = Новый Соответствие;
		Результат.Вставить("id", ИдентификаторЗапроса);
		Результат.Вставить("status", "received");
		
		РезультатJSON = ТелеграмАгентыКлиентСервер.ПреобразоватьВJSON(Результат);
		
		Ответ = Новый HTTPСервисОтвет(200);
		Ответ.УстановитьТелоИзСтроки(РезультатJSON);
		
	КонецЕсли;
			
	Возврат Ответ;
	
КонецФункции

Функция taskstatus_GET(Запрос)

	Идентификатор = Запрос.ПараметрыURL.Получить("id");
	
	Если Идентификатор = Неопределено Тогда
		Ответ = Новый HTTPСервисОтвет(400);
		Ответ.Причина = "request id not specified";
		Возврат Ответ;
	КонецЕсли;
	
	СтатусВнешнегоЗапроса = ТелеграмАгенты.СтатусВнешнегоЗапроса(Идентификатор);
	
	Если СтатусВнешнегоЗапроса = Неопределено Тогда
		Ответ = Новый HTTPСервисОтвет(404);
		Ответ.Причина = "request id not found";
		Возврат Ответ;
	КонецЕсли;
		
	Ответ = Новый HTTPСервисОтвет(200);
	Ответ.УстановитьТелоИзСтроки(СтатусВнешнегоЗапроса);
	Возврат Ответ;
	
КонецФункции

#КонецОбласти